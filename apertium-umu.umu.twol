Alphabet
A À Á B C D E È É F G H I Ì Í Ĭ J K L M N O Ò Ó P R S T U Ŭ Ù Ú V W X Y Z
a à á b c d e è é f g h i ì í ĭ j k l m n o ò ó p r s t u ŭ ù ú v w x y z

%{a%}:a %{a%}:á %{a%}:à %{a%}:0  
%{e%}:e %{e%}:é %{e%}:è %{e%}:0
%{i%}:i %{i%}:í %{i%}:ì %{i%}:ĭ %{i%}:0
%{o%}:o %{o%}:0
%{u%}:u %{u%}:0

%{U%}:u %{U%}:a %{U%}:à %{U%}:ŭ %{U%}:0
%{t%}:t %{t%}:d %{t%}:0
%{P%}:p %{P%}:b

%{N%}:n %{N%}:m
%{n%}:n %{n%}:0
%{h%}:h %{h%}:0
%{k%}:k %{k%}:0
%{l%}:l %{l%}:0
%{m%}:m %{m%}:0
%{w%}:w %{w%}:0
%{y%}:y %{y%}:0
%{W%}:w %{W%}:o 
%{A%}:a %{A%}:o 

%{'%}:0 %{'%}:́

%>:0
;

Sets

Vowel = A À Á E È É I Ì Í O Ò Ó U Ŭ Ù Ú 
        %{a%} a à á e è é i ì í o ò ó u ŭ ù ú ;

Consonant = B C D F G H J K L M N P R S T V W X Y Z
            b c d f g h j k l m n p r s t v w x y z ;

Rules

! Rules to shift accent for plurals
"Accent is placed in the penultimate vowel"
%{'%}:́  <=> _ [ :Vowel* :Consonant+ :Vowel+ :Consonant* ]/[ :0 ]* .#. ;

! Rule to add 'w' to end of 'shaa' when followed by word that starts with vowel

! Rules to make (nu)-(pa) contract to (mbà-)
"N is realized as m when the possessive prefix nu precedes pa"
%{N%}:m <=> _ :0* p: Vowel: ;

"P is realized as b when the possessive prefix nu precedes pa"
p:b <=> %{N%}: :0* _ Vowel: ;

"k is realized as g when the subject prefix nu precedes ka"
k:g <=> %{N%}: %{U%}: :0* _ Vowel: ;

"a is realized as à when the possessive prefix nu precedes pa"
%{'%}:̀  <=> [%{N%}: | k: | w: ] %{t%}: %{U%}: %>: p: a: _ ;
                except
                 _ [ :Vowel* :Consonant+ :Vowel+ :Consonant* ]/[ :0 ]* .#. ;

! Additional rule to make (ku)-(pa) contract to (kpà-) and (wu)-(pa) contract to (pà-)
"u is not realized when the possessive prefix ku or wu precedes pa"
%{U%}:0 <=> w: ( %{t%}: ) _ %>: [ p: | m: | k: ] Vowel: ;
        k: %{t%}: _ %>: [ p: | m: | k: ] Vowel: ;
        %{N%}: %{t%}: _ %>: [ p: | m: | k: ] Vowel: ;
        _ %>: Vowel: ;


"t realized when followed by -ah"
!%{t%}:0 <=> _ %{U%}: %>: \Vowel: ;
%{t%}:Cy <=> Cx: _ %{U%}: %>: a: [ h: | l: ] ;
                where Cx in ( %{N%} %{k%}  k  w )
                      Cy in ( d       t   t  t ) 
                matched ;


! Tree rule
!"N is realized as n when the possessive prefix nu precedes mi"
!%{N%}:n <=> _ %{t%}: %{U%}: %>: m: Vowel: ;

!"k realized as 0 when ku precedes stem that starts with k"
!%{k%}:0 <=> _ %{u%}: %>: k: Vowel: ;

"w not realized when (wu) + (mi)"
w:0 <=> _ ( %{t%}: ) [ u: | %{U%}: ] %>: m: Vowel: ;
        except
             _ %{t%}: %{U%}: %>: a: h: ;   

"u goes to ŭ when (ku) or (nu) + (mi)"
%{U%}:ŭ <=> k: %{t%}: _ %>: m: Vowel: ;
        %{N%}: %{t%}: _ %>: m: Vowel: ;

!"i is realized as ì when the possessive prefix precedes mi"
!i:í <=> %{N%}: %{t%}: %{U%}: %>: m: _ ;
!       k: %{t%}: %{U%}: %>: m: _ ;

"u is not realized if there is any number of consonants followed by a morpheme boundary followed by a vowel"
%{u%}:0 <=> _ :Consonant+ %>: :Vowel ;

"Stems ending in -xiin drop the final n when an imperative ending is added"
n:0 <=> :x :i :i _ %>: %{l%}: :0*;
        :x :i :i _ %>: %{o%}: %{o%}: k w ;

"Realize -oo when stem ends in consonant"
%{o%}:o <=> :Consonant %>: _ ;
            :Consonant %>: %{o%}: _ ;





! Pair Tests
!@ {N}:n {t}:0 {U}:ŭ >:0 m:m i:i h:h t:t {u}:0 k:k w:w 


! Resources:
! https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! https://wiki.apertium.org/wiki/Twol
! https://wiki.apertium.org/wiki/Hfst

