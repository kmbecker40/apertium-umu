# Morphological Transducer for Munsee

# Part of speech categories
# <n>      # Noun
# <v>      # Verb
# <iv>     # Intransitive Verb
# <tv>     # Transitive Verb
# <adj>    # Adjective
# <num>    # Numeral
# <aa>     # Animate Noun
# <nn>     # Inanimate Noun
# <prn>    # Pronoun
# <dem>    # Demonstrative
# <vaux>   # Auxiliary Verb
# <mod>    # Modal word
# <det>    # Determiner

# <sent>   # Sentence-ending punctuation
# <guio>   # Hyphen
# <cm>     # Comma
# <apos>   # Apostrophe
# <quot>   # Quotation mark
# <lquot>  # Left quotation mark
# <rquot>  # Right quotation mark
# <lpar>   # Left parenthesis
# <rpar>   # Right parenthesis

# Number morphology
# <pl>     # Plural
# <sg>     # Singular

# Grammatical Person
# <pers>   # Personal
# <p1>     # First Person
# <p2>     # Second Person
# <p3>     # Third Person

# Subject Marking
# <s_1sg>  # First person singular subject
# <s_2sg>  # Second person singular subject
# <s_3sg>  # Third person singular subject
# <s_1pl>  # First person plural subject
# <s_2pl>  # Second person plural subject
# <s_3pl>  # Third person plural subject

# Object Marking
# <o_1sg>  # First person singular object
# <o_2sg>  # Second person singular object
# <o_3sg>  # Third person singular object
# <o_1pl>  # First person plural object
# <o_2pl>  # Second person plural object
# <o_3pl>  # Third person plural object 

# Possession
# <px1sg>  # First person singular possessive
# <px2sg>  # Second person singular possessive
# <px3sg>  # Third person singular possessive
# <px1pl>  # First person plural possessive
# <px2pl>  # Second person plural possessive
# <px3pl>  # Third person plural possessive

# Miscellaneous Suffix Tags
# <abs>    # Absence
# <meat>   # Meat of
# <loc>    # Locative

# Other symbols
# >        # Morpheme boundary

###############################################################################
###                       M O R P H O T A C T I C S                         ###
###############################################################################

PATTERNS
## Example inflection pattern:
# NounRoot RegNounInfl
Animate-Nouns
Inanimate-Nouns
Intransitive-Verbs
Transitive-Verbs

PATTERN Animate-Nouns
:Animate-N-Infl(1)? Animate-N-Stems [<n><aa>:] Animate-N-Infl(2)? 

PATTERN Inanimate-Nouns
:Inanimate-N-Infl(1)? Inanimate-N-Stems [<n><nn>:] Inanimate-N-Infl(2)?

PATTERN Intransitive-Verbs
:Intransitive-V-Infl(1)? Intransitive-V-Stems [<v><iv>:] Intransitive-V-Infl(2)?

PATTERN Transitive-Verbs
:Transitive-V-Infl(1)? Transitive-V-Stems [<v><tv>:] Transitive-V-Infl(2)?

LEXICON Animate-N-Infl(2)
:    <pl>:>ak
:{N}{t}{U}> <px1sg>:>{u}{M}
:k{t}{U}> <px2sg>:>{u}{M}
:w{t}{U}> <px3sg>:>{u}{M}      
:w{t}{U}> <px1pl>:>{u}{M}>una
:{N}{t}{U}> <px2pl>:>{u}{M}>uwa
:k{t}{U}> <px3pl>:>{u}{M}>uwaawal
:{N}{t}{U}> <px1sg><pl>:>{u}{M}>ak
:k{t}{U}> <px2sg><pl>:>{u}{M}>ak
:w{t}{U}> <px3sg><pl>:>{u}{M}>al  


LEXICON Inanimate-N-Infl(2)
:    <pl>:>al
:{N}{t}{U}> <px1sg>:>{u}{M}
:k{t}{U}> <px2sg>:>{u}{M}
:w{t}{U}> <px3sg>:>{u}{M}     
:w{t}{U}> <px1pl>:>{u}{M}>una
:{N}{t}{U}> <px2pl>:>{u}{M}>uwa
:k{t}{U}> <px3pl>:>{u}{M}>uwaawal
:{N}{t}{U}> <px1sg><pl>:>{u}{M}>al
:k{t}{U}> <px2sg><pl>:>{u}{M}>al
:w{t}{U}> <px3sg><pl>:>{u}{M}>uwa 


LEXICON Intransitive-V-Infl(2)

# Intransitive Inanimate Verbs
:    <nn><s_sg>:>w
:    <nn><s_pl>:>wal

# Intransitive Animate Verbs
:n{u}> <aa><s_1sg>:>{M}
:k{u}> <aa><s_2sg>:>{M}
:   <aa><s_3sg>:>{w}     
:{n}{k}{u}> <aa><s_1pl>:>{o}>hn{a}
:k{u}> <aa><s_2pl>:>{o}>hmw{a}
:   <aa><s_3pl>:>{o}{o}>k
:   <aa><s_3pl>:>{w}{a}>k


LEXICON Transitive-V-Infl(2)

# Transitive Inanimate Verbs w/ Singular Object
:n{u}> <nn><s_1sg><o_sg>:>{u}n
:k{u}> <nn><s_2sg><o_sg>:>{u}n
:w{u}> <nn><s_3sg><o_sg>:>{u}n    
:{n}{k}{u}> <nn><s_1pl><o_sg>:>{u}>n{e}{e}n
:k{u}> <nn><s_2pl><o_sg>:>{u}>n{e}{e}w{a}
:w{u}> <nn><s_3pl><o_sg>:>{u}>n{e}{e}w{a}

# Transitive Inanimate Verbs w/ Plural Object
:n{u}> <nn><s_1sg><o_pl>:>{u}n>{a}l
:k{u}> <nn><s_2sg><o_pl>:>{u}n>{a}l   
:w{u}> <nn><s_3sg><o_pl>:>{u}n>{a}l  
:{n}{k}{u}> <nn><s_1pl><o_pl>:>{u}>n{e}{e}n
:k{u}> <nn><s_2pl><o_pl>:>{u}>n{e}{e}w{a}
:w{u}> <nn><s_3pl><o_pl>:>{u}>n{e}{e}w{a}

# Transitive Animate Verbs w/ Singular Object
:n{u}> <aa><s_1sg><o_3sg>:>{a}{a}w
:n{u}> <aa><s_1sg><o_3sg>:>{a}
:k{u}> <aa><s_2sg><o_3sg>:>{a}{a}w  
:k{u}> <aa><s_2sg><o_3sg>:>{a}
:w{u}> <aa><s_3sg><o_3sg>:>{a}{a}w{a}l
:{n}{k}{u}> <aa><s_1pl><o_3sg>:>{a}{a}w{u}n{a}
:k{u}> <aa><s_2pl><o_3sg>:>{a}{a}w{a}
:w{u}> <aa><s_3pl><o_3sg>:>{a}{a}w{a}{a}w{a}l
:w{u}> <aa><s_3pl><o_3sg>:>{a}{a}w{a}l

# Transitive Animate Verbs w/ Plural Object
:n{u}> <aa><s_1sg><o_3pl>:>{a}{a}w{a}k
:k{u}> <aa><s_2sg><o_3pl>:>{a}{a}w{a}k 
:w{u}> <aa><s_3sg><o_3pl>:>{a}{a}w{a}l
:{n}{k}{u}> <aa><s_1pl><o_3pl>:>{a}{a}w{u}n{a}
:k{u}> <aa><s_2pl><o_3pl>:>{a}{a}w{a}
:w{u}> <aa><s_3pl><o_3pl>:>{a}{a}w{a}{a}w{a}l


# problems
# 4 - stuff that used to analyze isnt, so the forms aren't the issue. check vowel definitions.

# transducer analyzing "grandmother" correctly, taking accents too but not for "trees" or "chairs."
# probably problem with spelling



###############################################################################
###                          L E X I C O N                                  ###
###############################################################################

## Example stem lexicon:
# LEXICON NounRoot
# house:house   # A noun

LEXICON Animate-N-Stems
pámbiil:p{a}mb{i}{i}l             # "book"
moonáhkeew:m{o}{o}n{a}hk{e}{e}w       # "groundhog"
pootaatíikan:p{o}{o}t{a}{a}t{i}{i}k{a}n   # "horn"
óohŭm:{o}{o}h{u}m                 # "grandmother"
moxóomus:m{o}x{o}{o}m{u}s           # "grandfather"
míhtkuw:m{i}htk{u}w                    # "tree"
áhpapoon:{a}hp{a}p{o}{o}n         # "chair"

LEXICON Inanimate-N-Stems
paxkshíikan:p{a}xksh{i}{i}k{a}n     # "knife"
pakíinjuw:p{a}k{i}{i}nj{u}w         # "plate"
wííkwahm:w{i}{i}kw{a}hm           # "house"
mbúy:mb{u}y                           # "water"
áanay:{a}{a}n{a}y                 # "road"
síipuw:s{i}{i}p{u}w               # "river"

LEXICON Intransitive-V-Stems
wíinee:w{i}{i}n{e}{e}           # "snow"
páasii:p{a}{a}s{i}{i}           # "swell"
kawíi:k{a}w{i}{i}               # "sleep"
alóhkee:al{o}hk{e}{e}           # "work"

LEXICON Transitive-V-Stems
néem:n{e}{e}m                   # "see"
míichii:m{i}{i}ch{i}{i}         # "eat"
ataal:{a}t{a}{a}l               # "want"


PATTERN Number
Numeral [<num>:]

LEXICON Numeral
/([0123456789])+/
/([0123456789])+[.,]([0123456789])+/

LEXICON Punctuation

.<sent>:.
..<sent>:..
...<sent>:...
;<sent>:;
\:<sent>:\:
!<sent>:!
-<guio>:-
—<guio>:—
,<cm>:,
?<sent>:? 
'<apos>:'
"<quot>:"
«<lquot>:«
»<rquot>:»
”<rquot>:”
“<lquot>:“
(<lpar>:(
\]<rpar>:\]
\[<lpar>:\[
)<rpar>:)
\\<sent>:\\
\/<sent>:\/


# Resources:
# https://github.com/apertium/lexd/blob/master/Usage.md
# https://wiki.apertium.org/wiki/List_of_symbols
# https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
# https://wiki.apertium.org/wiki/Lttoolbox_and_lexc
# https://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
# https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
# https://github.com/hfst/
# https://wiki.apertium.org/wiki/Hfst

